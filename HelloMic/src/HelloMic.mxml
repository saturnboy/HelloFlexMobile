<?xml version="1.0" encoding="utf-8"?>
<s:Application
	xmlns:fx="http://ns.adobe.com/mxml/2009" 
	xmlns:s="library://ns.adobe.com/flex/spark"
	xmlns:comps="comps.*"
	backgroundColor="#333333"
	applicationComplete="complete()">
	
	<fx:Script>
		<![CDATA[
			private static const DURATION:int = 5000; //5 seconds
			
			private var _mic:Microphone;
			
			private var _soundData:ByteArray;
			private var _sound:Sound;
			private var _channel:SoundChannel;
			
			private var _timeStart:Number;
			private var _timeEnd:Number;
			[Bindable] private var _pct:Number;
			
			private function complete():void {
				if (Microphone.isSupported) {
					_mic = Microphone.getMicrophone();
					_mic.rate = 44;
					_mic.gain = 60;
				}
			}
			
			/* **********  PLAY  ********** */
			private function play():void {
				_pct = 0.0;
				playBtn.enabled = recordBtn.enabled = false;
				
				_soundData.position = 0;
				_sound = new Sound();
				_sound.addEventListener(SampleDataEvent.SAMPLE_DATA, playbackDataHandler);
				_channel = _sound.play();
				_channel.addEventListener(Event.SOUND_COMPLETE, playbackComplete);
				
				addEventListener(Event.ENTER_FRAME, playbackTick);
			}
			
			private function playbackDataHandler(e:SampleDataEvent):void {
				for (var i:int = 0; i < 8192 && _soundData.bytesAvailable > 0; i++) {
					var f:Number = _soundData.readFloat();
					e.data.writeFloat(f);
					e.data.writeFloat(f);
				}
			}
			
			private function playbackTick(e:Event):void {
				_pct = _channel.position / DURATION;
			}
			
			private function playbackComplete(e:Event):void {
				_pct = 0;
				playBtn.enabled = recordBtn.enabled = true;
				
				removeEventListener(Event.ENTER_FRAME, playbackTick);
			}
			
			/* **********  RECORD  ********** */
			private function record():void {
				if (_mic != null) {
					playBtn.enabled = recordBtn.enabled = false;

					_soundData = new ByteArray();
					_mic.setSilenceLevel(0, DURATION);
					_mic.addEventListener(SampleDataEvent.SAMPLE_DATA, micDataHandler);
					
					_timeStart = new Date().time;
					_timeEnd = _timeStart + DURATION;
					_pct = 0.0;
					
					graph.reset();
					graph.xMax = DURATION;
					
					addEventListener(Event.ENTER_FRAME, recordTick);
				}
			}
			
			private function micDataHandler(e:SampleDataEvent):void {
				while(e.data.bytesAvailable) {
					var f:Number = e.data.readFloat();
					_soundData.writeFloat(f);
				}
			}
			
			private function recordTick(e:Event):void {
				var t:Number = new Date().time;
				if (t > _timeEnd) {
					_pct = 1.0;
					recordingComplete();
				} else {
					_pct = (t - _timeStart) / DURATION;
					
					var p:Point = new Point(t - _timeStart, _mic.activityLevel);
					if (p.y < 0) { p.y = 0 }
					graph.addPoint(p);
				}
			}
			
			private function recordingComplete():void {
				playBtn.enabled = recordBtn.enabled = true;
				
				removeEventListener(Event.ENTER_FRAME, recordTick);
				_mic.removeEventListener(SampleDataEvent.SAMPLE_DATA, micDataHandler);
				_mic.setSilenceLevel(100);
			}
		]]>
	</fx:Script>
	
	<s:VGroup left="20" right="20" top="0" bottom="60" gap="1" verticalAlign="middle">
		<comps:AreaGraph id="graph" width="100%" height="100"
						 backgroundColor="0x222222"
						 areaColor="0xff2299" />
		<comps:TimerBar id="timerBar" width="100%" height="3" percent="{_pct}"
						backgroundColor="0x666666"
						barColor="0xffffff" />
	</s:VGroup>
	
	<!-- button bar -->
	<s:Group left="0" right="0" bottom="0">
		<s:Rect left="0" right="0" top="0" bottom="0">
			<s:fill>
				<s:SolidColor color="#2299ff" />
			</s:fill>
		</s:Rect>
		<s:HGroup left="10" right="10" top="10" bottom="10">
			<s:Button id="playBtn" label="PLAY"
					  width="50%" minHeight="40"
					  click="play()"
					  enabled="false" />
			<s:Button id="recordBtn" label="RECORD"
					  width="50%" minHeight="40"
					  click="record()" />
		</s:HGroup>
	</s:Group>
</s:Application>